apply plugin: 'com.android.application'

tasks.matching {it instanceof Test}.all {
  testLogging.events = ["failed", "passed", "skipped"]
}

android {
  compileSdkVersion 25
  buildToolsVersion '25.0.3'

  defaultConfig {
    applicationId 'com.iotashome.friendspell'
    minSdkVersion 15
    targetSdkVersion 25
    versionCode 5
    versionName '1.1.1'
    vectorDrawables.useSupportLibrary = true

    testInstrumentationRunner 'com.iotashome.friendspell.util.MockTestRunner'
  }

  buildTypes {
    debug {
      testCoverageEnabled true
    }
  }

  productFlavors {
    canned {
    }
    normal {
    }
  }

  // Remove cannedRelease as it's not needed.
  android.variantFilter { variant ->
    if(variant.buildType.name.equals('release')
            && variant.getFlavors().get(0).name.equals('canned')) {
      variant.setIgnore(true);
    }
  }

  packagingOptions {
    exclude 'LICENSE.txt'
  }
}

final DAGGER_VERSION = '2.11'
final BUTTERKNIFE_VERSION = '8.7.0'
final GOOGLE_PLAY_SERVICES_VERSION = '11.0.2'
final SUPPORT_LIB_VERSION = '25.3.1'
final ESPRESSO_VERSION = '3.0.0'

dependencies {
  compile "com.google.dagger:dagger:${DAGGER_VERSION}"
  annotationProcessor "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"
  provided 'javax.annotation:javax.annotation-api:1.2'

  compile "com.android.support:appcompat-v7:${SUPPORT_LIB_VERSION}"
  compile "com.google.android.gms:play-services-plus:${GOOGLE_PLAY_SERVICES_VERSION}"
  compile "com.google.android.gms:play-services-nearby:${GOOGLE_PLAY_SERVICES_VERSION}"
  compile "com.google.android.gms:play-services-auth:${GOOGLE_PLAY_SERVICES_VERSION}"

  compile 'nl.qbusict:cupboard:2.2.0'
  compile 'com.google.code.gson:gson:2.8.1'
  compile 'com.squareup.picasso:picasso:2.5.2'

  compile 'com.jakewharton.timber:timber:4.5.1'
  compile "com.jakewharton:butterknife:${BUTTERKNIFE_VERSION}"
  annotationProcessor "com.jakewharton:butterknife-compiler:${BUTTERKNIFE_VERSION}"

  testCompile 'junit:junit:4.12'

  androidTestCompile ('com.android.support.test:runner:1.0.0') {
    exclude module: 'support-annotations'
  }
  androidTestCompile ('com.android.support.test:rules:1.0.0') {
    exclude module: 'support-annotations'
  }
  androidTestCompile ("com.android.support.test.espresso:espresso-core:${ESPRESSO_VERSION}") {
    exclude module: 'support-annotations'
  }
  androidTestCompile ("com.android.support.test.espresso:espresso-intents:${ESPRESSO_VERSION}") {
    exclude module: 'support-annotations'
  }

  androidTestCompile 'org.mockito:mockito-android:2.8.47'
  androidTestAnnotationProcessor "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"
}

apply plugin: 'com.google.gms.google-services'